resource "github_repository" "example" {
  name             = "example"
  description      = "example"
  auto_init        = false

  allow_merge_commit     = false
  delete_branch_on_merge = true
  has_issues             = true
  topics                 = local.common_topics

  lifecycle {
    prevent_destroy = true
  }

  template {
    owner = var.github_organization
    repository = "dataworks-repo-template"
  }
}

resource "github_team_repository" "example_dataworks" {
  repository = github_repository.example.name
  team_id    = github_team.dataworks.id
  permission = "push"
}

resource "github_branch_protection" "example_master" {
  branch         = github_repository.example.default_branch
  repository     = github_repository.example.name
  enforce_admins = true

  required_status_checks {
    strict = true
  }

  required_pull_request_reviews {
    dismiss_stale_reviews      = true
    require_code_owner_reviews = true
  }
}

resource "github_issue_label" "example" {
  for_each   = { for common_label in local.common_labels : common_label.name => common_label }
  color      = each.value.colour
  name       = each.value.name
  repository = github_repository.example.name
}

resource "null_resource" "example" {
  triggers = {
    repo = github_repository.example.name
  }
  provisioner "local-exec" {
    command = "./initial-commit.sh ${github_repository.example.name} '${github_repository.example.description}' ${github_repository.example.template.0.repository}"
  }
}

resource "github_actions_secret" "example_terraform_version" {
  repository      = github_repository.example.name
  secret_name     = "TERRAFORM_VERSION"
  plaintext_value = var.terraform_12_version
}
